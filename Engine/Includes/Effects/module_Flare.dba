//////////////////////////////////////////////////////////////
//  Game Engine Standard Modules - FLARE MODE
//  Concept and code by thenerd, Summer 2013!
//  Subsection of Module_Entity
//////////////////////////////////////////////////////////////

function Entity_Flare_Init():
    global Flare_Image as integer
    //give it a high number so it renders
    Flare_Image = 8000
    load image "Media\Materials\Engine\sunflare2.png",Flare_Image
    global Flare_Effect as integer
    Flare_Effect = reserve free effect()
    load effect "Media\Shaders\BlendAdditiveComplex.fx",Flare_Effect,0
    
ENDFUNCTION

function Entity_Flare_Add(x#,y#,z#):
    //add new entity
    i = 0
    while ent_Getexists("env_flare_"+str$(i))
        i = i + 1
    endwhile
    entAdd("env_flare_"+str$(i))
    ref = entgetreference("env_flare_"+str$(i))
    entAddAttribute(ref,"name","env_flare_"+str$(i))
    entAddAttribute(ref,"type","env_flare")
    entAddAttribute(ref,"x",str$(x#))
    entAddAttribute(ref,"y",str$(y#))
    entAddAttribute(ref,"z",str$(z#))
    entAddAttribute(ref,"enabled",str$(1))
    entAddAttribute(ref,"texture","Media\Materials\Engine\sunflare2.png")
ENDFUNCTION

function Entity_Flare_Update(i):
    if Ent_GetInt(i,"modelid")=0:
    modelid = reserve free object()
    textureid = reserve free image()
    EntAddAttribute(i,"modelid",str$(modelid))
    EntAddAttribute(i,"textureid",str$(textureid))
    EntAddAttribute(i,"rr","0")
    EntAddAttribute(i,"fx",str$(reserve free effect()))
    EntAddAttribute(i,"alpha","1.0")
    endif
    if object exist(Ent_GetInt(i,"modelid"))=0:
        make object plain Ent_GetInt(i,"modelid"),4,4
        load image EntGetAttribute(i,"texture"),Ent_GetInt(i,"textureid")
        texture object Ent_GetInt(i,"modelid"),Ent_GetInt(i,"textureid")
        load effect "Media\Shaders\BlendAdditiveComplex.fx",Ent_GetInt(i,"fx"),0
        set object effect Ent_GetInt(i,"modelid"),Ent_GetInt(i,"fx")
    endif
    set object to camera orientation Ent_GetInt(i,"modelid")
    position object Ent_GetInt(i,"modelid"),Ent_GetFloat(i,"x"),Ent_GetFloat(i,"y"),Ent_GetFloat(i,"z")
    rr_new#=wrapvalue(Ent_GetFloat(i,"rr")+((10+rnd(10))*TBM))
    EntSetAttribute(i,"rr",str$(rr_new#))
    scale# = 100+(sin(rr_new#)*4)
    scale object Ent_GetInt(i,"modelid"),scale#,scale#,scale#
    
    enabled = Ent_GetInt(i,"enabled")
    oldx#=Ent_GetFloat(i,"x")
    oldy#=Ent_GetFloat(i,"y")
    oldz#=Ent_GetFloat(i,"z")
    x#=camera position x()
    y#=camera position y()
    z#=camera position z()
    collide=SC_rayCastGroup(1,oldx#,oldy#,oldz#,x#,y#,z#,0)
    alpha#=Ent_GetFloat(i,"alpha")
    if collide>0
    rem get the collision point            
    newx#=SC_getStaticCollisionX()
    newy#=SC_getStaticCollisionY()
    newz#=SC_getStaticCollisionZ()
    rem get collision normal
    normx#=SC_getCollisionNormalX()
    normy#=SC_getCollisionNormalY()
    normz#=SC_getCollisionNormalZ()
    alpha_target# = 0.0 
    else
    alpha_target# = 0.0
    if enabled = 1 then alpha_target# = 1.0
    endif
    alpha# = TBM_Curvevalue(alpha_target#,alpha#,15.0)
    EntSetAttribute(i,"alpha",str$(alpha#))
    set effect constant float Ent_GetInt(i,"fx"),"Alpha",alpha#
ENDFUNCTION

function Entity_Flare_Reload(i):
    modelid = Ent_GetInt(i,"modelid")
    textureid = Ent_GetInt(i,"textureid")
    fxid = Ent_GetInt(i,"fx")
    delete object modelid
    delete image textureid
    
    make object plain modelid,4,4
    load image EntGetAttribute(i,"texture"),textureid
    texture object Ent_GetInt(i,"modelid"),textureid
    load image EntGetAttribute(i,"texture"),textureid
    texture object modelid,textureid
    set object effect modelid,fxid
endfunction

function Entity_Flare_Delete(i):
    modelid = Ent_GetInt(i,"modelid")
    textureid = Ent_GetInt(i,"textureid")
    fxid = Ent_GetInt(i,"fx")
    delete object modelid
    delete image textureid
    delete effect fxid
endfunction

function Entity_Flare_Terminate():
    
ENDFUNCTION

//////////////////////////////////////////////////////////////
// End of code
//////////////////////////////////////////////////////////////