//////////////////////////////////////////////////////////////
//   OpenFPS
//   MAIN SOURCE
//////////////////////////////////////////////////////////////
//
// ----- Changelog:
// (11/19/09) - BMacZero - Created.
// (01/17/13) - thenerd  - Big project revival. 
//
// ----- To-Do / Notes:
// This is the main source file for the OpenFPS Project.
//
//////////////////////////////////////////////////////////////
// Modules:
//
// *TO-DO LIST*
// Ordered in order of priority.
// -module_Input
// -module_Camera
// -module_World
// -module_Terrain
// -module_Network
// -?
//
//////////////////////////////////////////////////////////////
// Setup program chunks
// This serves as organization. Each chunk is a list of functions.
// The function CORE_Run_Chunk() will run the list of functions
// within the specified chunk. In the future, options will be
// created allowing a function chunk to be run in multithreaded
// or debug mode.
//////////////////////////////////////////////////////////////

//set up arrays to store chunks
Dim Chunk_Init() As Dword
Dim Chunk_Update() As Dword
Dim Chunk_Terminate() As Dword

//store arrays in memory
Global Chunk_Init_PTR As dword
Global Chunk_Update_PTR As dword
Global Chunk_Terminate_PTR As dword
Chunk_Init_PTR = Get Arrayptr(Chunk_Init()) : Unlink Array Chunk_Init()
Chunk_Update_PTR = Get Arrayptr(Chunk_Update()) : Unlink Array Chunk_Update()
Chunk_Terminate_PTR = Get Arrayptr(Chunk_Terminate()) : Unlink Array Chunk_Terminate()

//Init chunk
//core modules:
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"SYS_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"TBM_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"Input_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"Ent_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"Audio_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"UI_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"Network_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"Game_Init")

//Update chunk
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"TBM_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"Input_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"Network_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"Game_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"Audio_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"UI_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"Ent_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"SYS_Sync")

//Terminate chunk
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"TBM_Terminate")
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"Input_Terminate")
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"Audio_Terminate")
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"Game_Terminate")
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"Network_Terminate")
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"UI_Terminate")
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"SYS_Terminate")

//////////////////////////////////////////////////////////////
// Init
//////////////////////////////////////////////////////////////

//runs the init set of functions
CORE_Chunk_Run(Chunk_Init_PTR)

Engine_Check_EditorFlag()

//////////////////////////////////////////////////////////////
// Main loop
//////////////////////////////////////////////////////////////

While Game.Terminate = 0
    //runs the update set of functions
    CORE_Chunk_Run(Chunk_Update_PTR)
Endwhile

//////////////////////////////////////////////////////////////
// Terminate
//////////////////////////////////////////////////////////////

//runs the terminate set of functions and then exits
CORE_Chunk_Run(Chunk_Terminate_PTR) : End

//////////////////////////////////////////////////////////////
// CHUNK Functions
//////////////////////////////////////////////////////////////

//CORE_Chunk_Add - Adds a function to the PTR chunk specified.
Function CORE_Chunk_Add(chunkptr As dword,functionname as string):
    local newchunkptr as dword
    local dim temp_chunk() as dword
    Link Array temp_chunk(),chunkptr
    Array Insert At Bottom temp_chunk()
    newchunkptr = get arrayptr(temp_chunk())
    temp_chunk()=Get Ptr To Function(functionname)
    unlink array temp_chunk()
Endfunction newchunkptr

//CORE_Chunk_Run - Executes the specified chunk of functions.
Function CORE_Chunk_Run(chunkptr As dword):
    Local Dim temp_chunk() As dword
    Link Array temp_chunk(),chunkptr
    For func = 0 To Array Count(temp_chunk()):
        //execute function string for Chunk(func)
        Call Function Ptr (temp_chunk(func))
    Next func
    unlink array temp_chunk()
    Undim temp_chunk()
endfunction

//////////////////////////////////////////////////////////////
// End of code
//////////////////////////////////////////////////////////////