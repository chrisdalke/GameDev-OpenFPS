// TERRAIN MODULE

// -- GLOBALS --
function InitTerrain(ReloadFlag)

	t_Heightmap = 141
	t_Texture   = 142
	t_Detail    = 143
	
	if file exist("debug.txt")=1 then delete file "debug.txt"
	open to write 1,"debug.txt"
	
	write string 1,"DEBUG: Terrain start load"
	
	if ReloadFlag<1:
		write string 1,"DEBUG: Loading heightmaps"
		load image "data\heightmap.bmp",t_Heightmap
		load image "data\texture.jpg",t_Texture
		load image "data\detail.tga",t_Detail
		write string 1,"DEBUG: heightmaps loaded"
	else:
		write string 1,"DEBUG: deleting terrain"
		BT DeleteTerrain t_Object
		write string 1,"DEBUG: Terrain deleted"
	endif
	
	write string 1,"DEBUG: deleting DB object"
	if object exist(1)>0 then delete object 1
	
	write string 1,"DEBUG: making new terrain"
	t_Object=BT MakeTerrain()
	
	t_ObjectDB=1
	
	write string 1,"DEBUG: setting terrain images"
	BT SetTerrainHeightmap t_Object,t_Heightmap
	BT SetTerrainTexture t_Object,t_Texture
	BT SetTerrainDetail t_Object,t_Detail
	
	write string 1,"DEBUG: setting terrain LOD"
	BT SetTerrainLOD t_Object,3
	BT SetTerrainLODDistance t_Object,1,1000.0
	BT SetTerrainLODDistance t_Object,2,2000.0
	
	write string 1,"DEBUG: Terrain smoothing/quads"
	BT SetTerrainSmoothing t_Object,1
	BT SetTerrainQuadRotation t_Object,1
	
	write string 1,"DEBUG: Terrain scale"
	//Set some other values
	BT SetTerrainScale t_Object,1.2
	BT SetTerrainYScale t_Object,0.2
	BT SetTerrainSplit t_Object,8
	BT SetTerrainDetailTile t_Object,3.0
	
	write string 1,"DEBUG: building terrain"
	if ReloadFlag<1:
		BT BuildTerrain t_Object,t_ObjectDB
	else:
		BT BuildTerrain t_Object,t_ObjectDB+2000
	endif
	BT SetBuildStep 0	
	repeat
		write string 1,"DEBUG: build step"
		progress=BT ContinueBuild()
	until progress=-1
	
	write string 1,"DEBUG: BUILD SUCCESSFUL!"
	write string 1,"DEBUG: setting collision"
	SetTerrainCollision()
	write string 1,"DEBUG: Collision set"
	close file 1
endfunction

function SetTerrainCollision()
	LODLevel=0
	DIM terrainSectors(BT GetSectorCount(t_Object,LODLevel)-1) as dword
	for i=0 to BT GetSectorCount(t_Object,LODLevel)-1
	    if BT GetSectorExcluded(t_Object,LODLevel,i)=0
	        terrainSectors(i)=find free object(602,262144)
	        BT MakeSectorObject t_Object,LODLevel,i,terrainSectors(i)
	        position object terrainSectors(i),BT GetSectorPositionX(t_Object,LODLevel,i),BT GetSectorPositionY(t_Object,LODLevel,i),BT GetSectorPositionZ(t_Object,LODLevel,i)
			sc_setupcomplexobject terrainSectors(i),0,4
	        exclude object on terrainSectors(i)
	    endif
	next i
endfunction

function UpdateTerrain()
		BT SetCurrentCamera 0
		BT UpdateTerrainCull t_Object
		BT UpdateTerrainLOD t_Object
		BT RenderTerrain t_Object
endfunction