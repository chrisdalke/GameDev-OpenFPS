remstart
/////////////////////////////////////////////////////
//
//   OpenFPS
//   MAIN SOURCE
//
//
/////////////////////////////////////////////////////

----- Changelog (main source file only):
(11/19/09) - BMacZero - Created
(11/23/09) - BMacZero - Added menu
(11/24/09) - BMacZero - [Removed BlitzTerrain temporarily]
(2/7/10)   - thenerd  - Changed screen res to 1024x768x32 fullscreen, worked on menu functions
(2/15/10)  - BMacZero - Updated BlitzTerrain code for BT 2.0
(2/25/10)  - BMacZero - Globalized timerbased system, added shooting
(2/28/10)  - BMacZero - Removed BT temporary code
(2/28/10)  - Kaedroho - Position mouse to centre of the screen added. This stops the program from exiting when using multiple monitors.
(6/23/10)  - miso     - Replaced 2 standard text command to the new UI_Text command, yet using placeholder fonts
(6/23/10)  - miso     - Added an UI_CenterText command, just to show how it works, can be deleted
(6/26/10)  - miso     - Wrote a To-Do note.
(6/27/10)  - thenerd  - Added some effects to the menu background.
(6/27/10)  - thenerd  - Added some effects to the menu background.
(6/27/10)  - miso     - Repositioned the texts to give space for the appearing console
(6/27/10)  - miso     - Added variable SyS_Event_Terminate, to properly jump to the
                        terminate functions part. Set this to 1 of you want to terminate
                        instead of an "end" command. I put it here, because I dont know which
                        module would fit best to contain it. Reposition it if you have a better idea,
                        or I suggest to create a module_core or module_System, 
                        so I should put my debug commands there too.
                      - Added label _terminate:
(7/29/10)  - thenerd  - Added to game engine.
(11/28/10) - thenerd  - Restructured game engine code to be more clear (now in module_engine)

----- To-Do / Notes:
Important note for all modules: don't use mousemovex() and mousemovey() commands, it clears the mouse movement buffer.
                                Instead, use SyS_MouseMoveX and SyS_MouseMoveY integer variables if needed,
                                they get their value every loop. (miso.)
                                
----- Modules
MODULE_MAIN.dba			- Core module (this one) 					(prefix MAIN)
MODULE_MENU.dba			- Handles all menu functionality			(prefix MENU)
MODULE_UI.dba			- Handles user interface elements			(prefix UI)
 MODULE_UI_CHATBOX.dba  - UI sub-module, text boxes					(prefix UI)
 MODULE_UI_CONSOLE.dba  - UI sub-module, console menu				(prefix UI)
 MODULE_UI_CREDITS.dba  - UI sub-module, credits menu				(prefix UI)
 MODULE_UI_OPTIONS.dba  - UI sub-module, options menu				(prefix UI)
MODULE_AI.dba			- Artificial intelligence system			(prefix AI)
MODULE_ENGINE.dba		- Core game engine module					(prefix CORE)
MODULE_MOVEMENT.dba		- Movement system							(prefix MVM)
MODULE_NETWORK.dba		- Multiplayer and network functionality		(prefix NET)
MODULE_PHYSICS.dba		- Uses Newton DLL for physics system		(prefix PHY)
MODULE_RESOURCE.dba		- Handles all resources for modules			(prefix RES)
MODULE_SHOOTING.dba		- Handles weapons and shooting mechanics	(prefix GUN)
MODULE_TERRAIN.dba		- Handles terrain system with Blitzterrain	(prefix LAND)
MODULE_VISUALS.dba		- Handles all visual effects and systems 	(prefix VIS)


remend

`==========Globals==========`
Global SyS_Event_Terminate as Boolean : `set this variable to 1 to force the main module
                                      : `to run the terminate functions. 
                                      : `(I need it to close my logfile)
Global SyS_LoopMode as string		  :	`Variable to determine which modules to update in loop
Global SyS_TimeFactor as float		  :	`Variable for Timer based engine operations
									  :	`Updated every loop with an elapsed time variable
Global SyS_TimeMultiplier as float    : `Time Multiplier factor
Global SyS_GameSpeed as float		  :	`Speed variable for game loops
Global CORE_DebugScreen as Boolean    : `Debug setting
									  
/////////////////////////////////////////////////////////
// INIT                                                //
/////////////////////////////////////////////////////////

`set up display

VIS_Setup_LoadSettings() `Replaces set display mode 1024,768,screen depth()

sync on
sync rate 0

`various settings
autocam off
randomize timer()
hide mouse
disable escapekey

`backdrop colour
backdrop on

`color backdrop rgb(200,200,255)  `light blue
color backdrop rgb(0,0,0)		  `black

`INITIATE CORE MODULES

`resource module
RES_init()

`loading screen
MENU_LoadingScreen()

`engine modules
UI_init() 	`ui module
MVM_init()  `movement module
CORE_init()	`game core modules
MENU_init() `menu module
`NET_init()	`network module
ENT_Init()	`entity module
LVL_Init()  `level module


`load up various configurations
if file exist("_PlayerData.cfg")<1:
	open to write 16,"_PlayerData.cfg"
	write string 16,"setplayername Unnamed Player"
	close file 16
endif
UI_SUB_ParseFile("_PlayerData.cfg")


/////////////////////////////////////////////////////////
// RUN                                                 //
/////////////////////////////////////////////////////////

`menu mode
SyS_LoopMode="menu"

`time multiplier
SyS_TimeMultiplier=1

`run main loop
while SyS_Event_Terminate<1

	`Update System timer
	SyS_TimeFactor=CORE_TimeFactor()*SyS_TimeMultiplier
	
	select SyS_LoopMode
		`CORE LOOPS
		case "menu"
			`MENU LOOP
			// Update menu
			MENU_Show()
			MENU_Update()
		endcase
		case "engine"
			`GAME ENGINE LOOP
			// Update game engine
			CORE_update()
		endcase
		case "credits"
			MENU_Hide()
			`credits loop
			
			texture object MENU_FGObject,MENU_creditsPolishBG
			`paste image MENU_creditsPolishBG,0,0
			paste image MENU_credits,(UI_Display.MiddleWidth-400),(UI_Display.Height-MENU_credits_Y),1
			MENU_credits_Y=MENU_credits_Y+0.3*SyS_TimeFactor
			if MENU_credits_Y>100 then UI_text(1,-2,4,"Press [Escape] to return to the menu...")
			if MENU_credits_Y>2500 or escapekey():
		 		Credits_FadeOut()
			endif
		endcase
	endselect
	
	`do general stuff
	position mouse UI_Display.MiddleWidth,UI_Display.MiddleHeight `center windows mouse
	
	`debug
	
	`handle UI system
	UI_Handle()
	
	`sync to the screen
	`Note: would there be any advantages to using fastsync for the menu?
	sync
	
	`destroy temporary gui elements
	MENU_DestroyButtons()
	
endwhile

/////////////////////////////////////////////////////////
// TERMINATE                                           //
/////////////////////////////////////////////////////////

`end modules
CORE_TerminateAll()

`end of code
/////////////////////////////////////////////////////////