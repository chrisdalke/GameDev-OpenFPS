remstart
/////////////////////////////////////////////////////
//
// OpenFPS NETWORK FUNCTIONS Core Module
//
// Created by --
// Maintained by --
//
/////////////////////////////////////////////////////

----- Changelog:
(2/8/10) - BMacZero - Created
(07/06/10) - miso     - just placed logger commands
(07/06/10) - thenerd  - added commands
(07/28/10) - thenerd  - added PlayerData.cfg for player name & other info
(08/17/10) - miso     - added a display info when waiting to connect master server, not to see tha black screen

----- To-Do:
-None-

----- Purpose:
Communicates with host to retrieve updated information on
player, enemy, and object positions and states.
Passes this information to relevant modules to deal with it.


----- Dependancies and Conflicts (IMPORTANT STUFF):
-None-

----- Other Notes:
Chat Message Tags
&name=name           - Changes your name and displays a message.
&name_silent=name    - Changes your name without displaying a message.
&color="r,g,b"		 - Changes your chat color (WORK IN PROGRESS)
&blarg	             - :D

----- Function List:
-Client:
initNetwork()			  - initializes the networking module
updateNetwork()     	  - updates any network data with the server.
terminateNetwork()  	  - ends the networking module, and cleans up any used resources.
getChatMessage()		  - gets the latest chat message.
sendChatMessage(string$)  -	sends a chat message.
-Server:
-NONE-

remend

`-------GLOBALS----------
#constant SERVER_HOST "96.233.35.41"
#constant SERVER_PORT 27013
Global channel as DWORD
Global PlayerName$ as string

/////////////////////////////////////////////////////////
// INIT                                                //
/////////////////////////////////////////////////////////


function initNetwork()
UI_WriteLog(UI_Yellow,"=== Starting Network module")
global net_disable
net_disable=0
winsock make
if (winsock error())
   `an error has occurred, output the error message to the log and quit.
   UI_WriteLog(UI_RED,winsock error msg$())
   winsock clean up
   end
else
	UI_WriteLog(UI_Green,"Connected to the master server "+SERVER_HOST+":"+STR$(SERVER_PORT))
endif

sync
`UI_Centertext(2,512,300,"Connecting to Master Server..."):sync
`UI_Centertext(2,512,300,"Connecting to Master Server..."):Sync

channel = winsock connect(SERVER_HOST, SERVER_PORT)
check_for_error(channel)

if net_disable<1:
	UI_WriteLog(UI_Green,"=== Network module started")

	UI_WriteLog(UI_Yellow,"=== Loading Player Data")
	temp_plr_data=grabResource(res_File)
	OPEN TO READ temp_plr_data,"PlayerData.cfg"
	READ STRING temp_plr_data,PlayerName$
	PlayerName$=right$(PlayerName$,(len(PlayerName$)-11))
	close file temp_plr_data
	freeResource(temp_plr_data,res_File)
	UI_WriteLog(UI_Green,"=== Player Data Loaded")


	UI_Addtexttochatbox("Welcome to Biostorm: Chemical Warfare!")
	
	UI_Addtexttochatbox("Master server is online.")
	sendPlayerName(PlayerName$,0)
else:
	UI_Addtexttochatbox("Master server is offline!")
	UI_Addtexttochatbox("No chat will be available.")
endif
`delete sprite NetConnectSprite
endfunction


/////////////////////////////////////////////////////////
// CORE                                                //
/////////////////////////////////////////////////////////

function updateNetwork()
   if net_disable<1:
  	 	winsock refresh channel channel
   		check_for_error(channel)
   		getChatMessage()
   		`sendPlayerName("&name="+PlayerName$)
   endif
endfunction

function getChatMessage():
if net_disable<1:
   if (winsock channel data waiting(channel))
      check_for_error(channel)
      message$ = winsock recv string(channel)
      if (message$ = "")
         `this client has been disconnected remotely by the server.
         if (winsock channel error(channel))
            UI_Addtexttochatbox("You have been forcefully disconnected by the server!")
         else
            UI_Addtexttochatbox("You have been gracefully disconnected by the server!")
         endif
      else
		UI_Addtexttochatbox(message$)
      endif
   endif
endif
endfunction

function sendPlayerName(string$,mode):
if net_disable<1:
	//0=silent
	//1=normal
	if mode=0 then winsock send string channel, "&name_silent="+string$
	if mode=1 then winsock send string channel, "&name="+string$
	check_for_error(channel)
endif
endfunction

function sendChatMessage(string$):
if net_disable<1:
	winsock send string channel, string$
	check_for_error(channel)
endif
endfunction


/////////////////////////////////////////////////////////
// TERMINATE                                           //
/////////////////////////////////////////////////////////


function terminateNetwork()

UI_WriteLog(UI_Green,"=== Terminating module")
winsock clean up
UI_WriteLog(UI_Green,"=== Network module terminated")

endfunction

/////////////////////////////////////////////////////////
// DEBUG                                               //
/////////////////////////////////////////////////////////

function check_for_error(a_channel as DWORD)
`check for an error on a channel.
   if (winsock channel error(a_channel))
      `an error has occurred, enable the net_disable variable.
	  net_disable=1
   endif
endfunction