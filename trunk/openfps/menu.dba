remstart
/////////////////////////////////////////////////////
//
// OpenFPS MENU Module
//
// Created by thenerd
// Maintained by thenerd
//
/////////////////////////////////////////////////////

----- Changelog:

(11/19/09) - BMacZero - Adapted to existing code.
(2/7/10) - BMacZero   - Fixed issue with buttons highlighting incorrectly
(2/7/10) - thenerd    - Updating and changing menu system, adding gui functions
(2/8/10) - BMacZero   - Polished RenderWindow function, prefixed global vars with "menu"
(2/9/10) - thenerd    - Finished RenderWindow function with fill
(6/27/10)  - thenerd  - Added some effects to the menu background.
(6/27/10)  - miso     - Changed 2 mouseX() mouseY() commands to UI_MouseX() and UI_MouseY()
(6/27/10)  - thenerd  - Added color parameter to button function
(7/01/10)  - miso     - Hides ui_mouse before entering game mode
                        Creates a stamina bar, before jumping to game mode (won't crash the game if we delete this part)
(07/04/10) - miso     - Changed an end command to SyS_Event_Terminate=1

----- To-Do:
- Make buttons functional. DONE
- Add Custom GUI Elements/Functions:
	*Slider
	*Button - DONE
	*Checkbox
	*Window - DONE - Transparency?
	*Text Box


----- Purpose:
Runs the menu.


----- Dependancies and Conflicts (IMPORTANT STUFF):
-None-


----- Other Notes:
-None-


remend

/////////////////////////////////////////////////////////
// INIT                                                //
/////////////////////////////////////////////////////////

function initMenu()
`loading sprite
global menu_loadingimage
global menu_loadingsprite
menu_loadingimage  = grabResource(res_Image)
menu_loadingsprite = grabResource(res_Sprite)
load image "media\menu\loading-1.png",menu_loadingimage
sprite menu_loadingsprite,0,0,menu_loadingimage
set sprite priority menu_loadingsprite,1
global DebugScreen
DebugScreen=0
`main sprite
global menu_mainimage
global menu_mainsprite
menu_mainimage  = grabResource(res_Image)
menu_mainsprite = grabResource(res_Sprite)
load image "media\menu\main.png",menu_mainimage
sprite menu_mainsprite,0,0,menu_mainimage
show sprite menu_mainsprite

backdrop on
color backdrop rgb(0,0,255)
`autocam on

`background effects
global menu_bgfx1
menu_bgfx1  = grabResource(res_Object)
menu_bgfx1img  = grabResource(res_Image)
load image "media\menu\menu_bgfx1img.png",menu_bgfx1img
make object plain menu_bgfx1,1024,768
texture object menu_bgfx1,menu_bgfx1img
set object transparency menu_bgfx1,1
position object menu_bgfx1,0,0,512
point object menu_bgfx1,0,0,0

`music
global menu_music
menu_music  = grabResource(res_Music)
load music "media\audio\The Awakening.mp3",menu_music
play music menu_music
endfunction



/////////////////////////////////////////////////////////
// CORE                                                //
/////////////////////////////////////////////////////////

function runMenu()

local alpha as byte

`Transition out of loading
alpha=255
do

	alpha=alpha-20
	if alpha<20 then exit
	set sprite alpha menu_loadingsprite,alpha
	sync
loop
set sprite alpha menu_loadingsprite,0

`init gui variables
global menu_button1 as integer
global menu_button2 as integer
global menu_button3 as integer
menu_button1 = grabResource(res_Image)
menu_button2 = grabResource(res_Image)
menu_button3 = grabResource(res_Image)

global menu_left_top as integer
global menu_right_top as integer
global menu_top_flat as integer
global menu_bottom_flat as integer
global menu_left_bottom as integer
global menu_right_bottom as integer
global menu_left_flat as integer
global menu_right_flat as integer
menu_left_top = grabResource(res_Image)
menu_right_top = grabResource(res_Image)
menu_top_flat = grabResource(res_Image)
menu_bottom_flat = grabResource(res_Image)
menu_left_bottom = grabResource(res_Image)
menu_right_bottom = grabResource(res_Image)
menu_left_flat = grabResource(res_Image)
menu_right_flat = grabResource(res_Image)

`load gui images
load image "media\menu\gui\left_top.png", menu_left_top
load image "media\menu\gui\right_top.png", menu_right_top
load image "media\menu\gui\top_flat.png", menu_top_flat
load image "media\menu\gui\bottom_flat.png", menu_bottom_flat
load image "media\menu\gui\left_bottom.png", menu_left_bottom
load image "media\menu\gui\right_bottom.png", menu_right_bottom
load image "media\menu\gui\left_flat.png", menu_left_flat
load image "media\menu\gui\right_flat.png", menu_right_flat

global menu_clickstate as boolean

load image "media\menu\button-normal.png", menu_button1
load image "media\menu\button-hover.png",  menu_button2
load image "media\menu\button-clicked.png",menu_button3

`delete temporary main menu sprite
freeResource(menu_mainsprite,res_Sprite)

do


	`bg
	paste image menu_mainimage,0,0,1
	scroll object texture menu_bgfx1,0.001,0
	`buttons

		If button("<GAME ENGINE TEST>",78,219,rgb(255,100,100)) 
			
					//--Hide graphic mouse
						UI_HideMouse():`Hide mouse before entering game mode
					//--add a stamina bar before run game part
						aa=140:bb=97		
						UI_AddCenterGadgetToScreen("UI_staminabarholder",0+aa,0+bb,"UI_barholderimage")
					//--add the BAR for STAMINA
						UI_AddbarGadgetToScreen("UI_stamina",3+aa,3+bb,"UI_redbar",50)
						UI_CenterPositionGadget("UI_stamina",aa,bb)
						UI_addtitletogadget("UI_stamina",3,aa-8,bb-14,"STAMINA")

					//--add a FATIGUE bar before run game part (reverse of the stamina bar)
						aa=140:bb=137		
						UI_AddCenterGadgetToScreen("UI_fatiguebarholder",0+aa,0+bb,"UI_barholderimage")
					//--add the BAR for STAMINA
						UI_AddbarGadgetToScreen("UI_fatigue",3+aa,3+bb,"UI_orangebar",50)
						UI_CenterPositionGadget("UI_fatigue",aa,bb)
						UI_addtitletogadget("UI_fatigue",3,aa-8,bb-14,"FATIGUE")



					//--Jumps to the game mode	
						Exit `Campaign Mode
						
		Endif


	button("Versus Mode",78,279,rgb(255,255,255))
	button("Help",78,339,rgb(255,255,255))
	button("Options",78,399,rgb(255,255,255))
	button("Credits",78,459,rgb(255,255,255))
	If Button("Quit",78,519,rgb(255,255,255))=1 
		SyS_Event_Terminate=1
		Exit
	Endif
	
	if mouseclick()=0 then menu_clickstate=0
	
	if mouseclick()=1 then UI_FadeinMouse():if mouseclick()=2 then UI_FadeoutMouse()
	`RenderWindow(mousex(),mousey(),640,480)
	if DebugScreen=1:
		UI_Text(2,screen width()-50,screen height()-40,str$(screen fps())) 
	endif
	
    UI_Handle()
    
	sync
loop

delete object menu_bgfx1
freeResource(menu_bgfx1        ,res_Object)
freeResource(menu_mainimage    ,res_Image )
freeResource(menu_loadingsprite,res_Sprite)
freeResource(menu_loadingimage ,res_Image )

freeResource(menu_button1,res_Image)
freeResource(menu_button2,res_Image)
freeResource(menu_button3,res_Image)
freeResource(menu_left_top,res_Image)
freeResource(menu_right_top,res_Image)
freeResource(menu_top_flat,res_Image)
endfunction



/////////////////////////////////////////////////////////
// SUPPORT                                             //
/////////////////////////////////////////////////////////

function RenderWindow(x1,y1,x2,y2):

    if y2<y1
        temp=y2
        y2=y1
        y1=temp
    endif
    if x2<x1
        temp=x2
        x2=x1
        x1=temp
    endif

	paste image menu_left_top, x1,y1,1
	paste image menu_right_top,x2-image width(menu_right_top),y1,1
	tilex(menu_top_flat,x1+image width(menu_left_top),y1,((x2-image width(menu_right_top)*2)-x1)/image width(menu_top_flat))
	
	paste image menu_left_bottom,x1,y2,1
	paste image menu_right_bottom,x2-image width(menu_right_bottom),y2,1
	tilex(menu_bottom_flat,x1+image width(menu_left_bottom),y2,((x2-image width(menu_right_bottom)*2)-x1)/image width(menu_bottom_flat))
	
	tiley(menu_left_flat ,x1,y1+image height(menu_left_top),(y2-y1-image height(menu_left_bottom))/image height(menu_left_flat))
	tiley(menu_right_flat,x2-image width(menu_right_flat),y1+image height(menu_left_top),(y2-y1-image height(menu_left_bottom))/image height(menu_left_flat))
	
	ink rgb(0,0,0),0
	box x1+image width(menu_left_top),y1+image height(menu_left_top),x2-image width(menu_right_bottom),y2
	ink rgb(255,255,255),0
endfunction

function tilex(image,x,y,amount):
for z=0 to amount:
	paste image image,x+(z*image width(image)),y,1
next z
endfunction

function tiley(image,x,y,amount):
for z=0 to amount:
	paste image image,x,y+(z*image height(image)),1
next z
endfunction


function button(text$,x,y,color):

    local mouse as integer
    local clicked as boolean
	clicked=0
	
	select in_box2d(x,y,x+178,y+50)
        case 0
            paste image menu_button1,x,y,1
        endcase
        case 1
            paste image menu_button2,x,y,1
            if menu_clickstate=1 then clicked=1
        endcase
        case 2
            paste image menu_button3,x,y,1
            menu_clickstate=1
        endcase
    endselect
    ink color,0
	center text x+89,y+25-(text height("Oj")/2),text$
	
endfunction clicked



function in_box2d(x1,y1,x2,y2):

    local result as integer
    result=0
    local mx as integer
    local my as integer
	mx=UI_MouseX()
	my=UI_MouseY()

	if (mx >= x1) and (mx <= x2) and (my >= y1) and (my <= y2)
		if mouseclick()=0 then result = 1
		if mouseclick()=1 then result = 2
	endif

endfunction result 


`````````````````````````````````````````````
`SPLASH SCREEN (old code)
remstart
sync on
sync rate 60
backdrop on
color backdrop 0
load image "media\menu\logo.png",1
load image "media\menu\splash_bg.png",2
load sound "media\audio\logo.wav",1
spr_max=4
for x=1 to spr_max:
	sprite x,(screen width()/2)-(image width(1)/2),(screen height()/2)-(image height(1)/2),1
next x
timer1=0
range=300
wait 2000
do
	paste image 2,0,0
	timer1=timer1+1
	range=range-1
	set cursor 0,0
	print screen fps()
	print timer1
	if timer1>50 then alp=alp+1
	if timer1>60 and timer1<200 and sound playing(1)=0 then play sound 1
	if sound playing(1)=0 and trigger_done=1 then alp=alp-2
	if alp<0 then alp=0 
	if alp>255 then alp=255:trigger_done=1	
	for x=1 to spr_max:
		if range>2:
			rndx=rnd(range)-range/2
			rndy=rnd(range)-range/2
		endif
		if range<2 then rndx=0:rndy=0
		sprite x,(screen width()/2)-(image width(1)/2)+rndx,(screen height()/2)-(image height(1)/2)+rndy,1
		set sprite alpha x,alp 
	next x		
	sync
loop
remend