Rem ***** Included Source File *****
Remstart
/////////////////////////////////////////////////////
//
// OpenFPS USER INTERFACE SUBMODULE            
//
// Created by miso    
//
/////////////////////////////////////////////////////
`***** Included Source File *****
`----- Changelog:
`(06/27/10) - miso     - Created
`(07/04/10) - miso     - Changed an end command to SyS_Event_Terminate=1
`(12/29/10) - thenerd  - Added functionality for script parsing.

`----- To-Do:

`----- Purpose:
`Here are the console command check, use it to implement your own ingame console command.
`Maybe it can be useful to hide and show debug informations and such things.
`Also handles the parsing of scripts

`----- Dependancies and Conflicts (IMPORTANT STUFF):
`Uses the main user interface module. (module_ui.dba)

`----- Conflicts

`----- Other Notes:
`Usage (IMPORTANT***):

`Just put your own console command here, as the examples shows.

`----------MAIN  COMMANDS

`Too many, see UI_SUB_CheckConsoleCommand()

/////////////////////////////////////////////////////
Remend

/////////////////////////////////////////////////////
//MAIN COMMANDS
/////////////////////////////////////////////////////

Function UI_SUB_CheckConsoleCommand(console_result$)
//***************************************************************************************
//*   Miso   : how to add new console command?                                          *
//*     USAGE: Duplicate a line, change the text and change the function name           *
//*                                                                  to be called.      *
//*                                                                                     *
//***************************************************************************************
	console_resultupper$=Upper$(console_result$)
	command_exec=0
	
	select console_resultupper$
	case "HIDE MOUSE"
		MVM_HideMouse()
		command_exec=1
	endcase
	case "SHOW MOUSE"
		MVM_ShowMouse()
		command_exec=1
	endcase
	case "DEBUG"
		CORE_DebugScreen=1
		command_exec=1
	endcase
	case "MODE MENU"
		SyS_LoopMode="menu"
		command_exec=1
	endcase
	case "MODE ENGINE"
		SyS_LoopMode="engine"
		command_exec=1
	endcase
	case "TEST_CREDITS"
		MENU_StartCredits()
		command_exec=1
	endcase
	case "TEST_GAMESTATE"
		command_exec=1
	endcase
	case "EXIT"
		SyS_Event_Terminate=1
		command_exec=1
	endcase
	case "SYNC RATE 0"
		sync rate 0
		command_exec=1
	endcase
	case "SYNC RATE 15"
		sync rate 15
		command_exec=1
	endcase
	case "SYNC RATE 30"
		sync rate 30
		command_exec=1
	endcase
	case "SYNC RATE 60"
		sync rate 60
		command_exec=1
	endcase
	case "WELCOME"
		command_exec=1
	endcase
	case "PAUSE 1"
		CORE_GamePaused=1
		MVM_ShowMouse()
		command_exec=1
	endcase
	case "PAUSE 0"
		CORE_GamePaused=0
		MVM_HideMouse()
		command_exec=1
	endcase
	case "SMOOTH 1"
		UI_SetSmoothMouseOn(10)
	endcase
	case "SMOOTH 0"
		UI_SetSmoothMouseOff()
	endcase
	endselect
	if left$(console_resultupper$,5)="PARSE"
		parse_file$=right$(console_result$,len(console_result$)-6)
		UI_SUB_ParseFile(parse_file$)
		command_exec=1
	endif
	if left$(console_resultupper$,10)="MENU_STATE"
		MENU_state=right$(console_result$,len(console_result$)-11)
		command_exec=1
	endif
	if left$(console_resultupper$,4)="LOAD"
		LVL_Load(right$(console_result$,len(console_result$)-5))
		command_exec=1
	endif
	if left$(console_resultupper$,13)="SETPLAYERNAME"
		PlayerName$=right$(console_result$,len(console_result$)-14)
		
		UI_WriteLog(UI_Green,"Setting player name to "+PlayerName$)
		//set the player name here
		
		command_exec=1
	endif
	if left$(console_resultupper$,11)="FADER_ALPHA"
		alpha$=right$(console_result$,len(console_result$)-12)
		MENU_FaderAlpha=val(alpha$)
		command_exec=1
	endif
	if left$(console_resultupper$,10)="TIMEFACTOR"
		alpha$=right$(console_result$,len(console_result$)-11)
		SyS_TimeMultiplier=val(alpha$)
		command_exec=1
	endif
	if left$(console_resultupper$,3)="SAY"
		say_str$="~b"+PlayerName$+":~w "+right$(console_result$,len(console_result$)-4)
		`UI_Addtexttochatbox(say_str$)
		//NET_SendChatMessageTCP(say_str$)
		command_exec=1
	endif
	`clear the entry buffer if a command was executed
	if command_exec=1:
		Clear Entry Buffer:UI_Input.result = ""
		UI_Addtexttochatbox("CONSOLE Exec: ["+console_result$+"]")
	endif

Endfunction

function UI_SUB_ParseFile(file$):
	// -----------------------
	// Parser function
	// -----------------------
	`Parses a script file
	if file exist(file$)>0:
		global TEMP_file
		TEMP_file=RES_grab(RES_File)
		open to read TEMP_file,file$
		repeat
			read string TEMP_file,line$
			UI_SUB_CheckConsoleCommand(line$)
		until file end(TEMP_file)>0
		close file TEMP_file
		RES_free(TEMP_file,RES_File)
	endif
endfunction
