Rem ***** Included Source File *****
Remstart
/////////////////////////////////////////////////////
//
// OpenFPS USER INTERFACE SUBMODULE            
//
// Created by miso    
//
/////////////////////////////////////////////////////
`***** Included Source File *****
`----- Changelog:
`(06/27/10) - miso     - Created
`(07/04/10) - miso     - Changed an end command to SyS_Event_Terminate=1
`(12/29/10) - thenerd  - Added functionality for script parsing.

`----- To-Do:

`----- Purpose:
`Here are the console command check, use it to implement your own ingame console command.
`Maybe it can be useful to hide and show debug informations and such things.
`Also handles the parsing of scripts

`----- Dependancies and Conflicts (IMPORTANT STUFF):
`Uses the main user interface module. (module_ui.dba)

`----- Conflicts

`----- Other Notes:
`Usage (IMPORTANT***):

`Just put your own console command here, as the examples shows.

`----------MAIN  COMMANDS

`Too many, see UI_SUB_CheckConsoleCommand()

/////////////////////////////////////////////////////
Remend

/////////////////////////////////////////////////////
//MAIN COMMANDS
/////////////////////////////////////////////////////

Function UI_SUB_CheckConsoleCommand(console_result$)
//***************************************************************************************
//*   Miso   : how to add new console command?                                          *
//*     USAGE: Duplicate a line, change the text and change the function name           *
//*                                                                  to be called.      *
//*                                                                                     *
//***************************************************************************************
	console_result$=Upper$(console_result$)
	
	select console_result$
	case "HIDE MOUSE":
		MVM_HideMouse()
	endcase
	case "SHOW MOUSE":
		MVM_ShowMouse()
	endcase
	case "DEBUG"
		CORE_DebugScreen=1
	endcase
	case "MODE MENU"
		SyS_LoopMode="menu"
	endcase
	case "MODE ENGINE"
		SyS_LoopMode="engine"
	endcase
	case "EXIT"
		SyS_Event_Terminate=1
	endcase
	case "SYNC RATE 0"
		sync rate 0
	endcase
	case "SYNC RATE 15"
		sync rate 15
	endcase
	case "SYNC RATE 30"
		sync rate 30
	endcase
	case "SYNC RATE 60"
		sync rate 60
	endcase
	case "START SERVER"
		NET_StartServer()
	endcase
	case "WELCOME"
		NET_Server.DATA_Welcome=1
	endcase
	case "WIREFRAME ON"
		set object wireframe NET_Server.DATA_MapObj,1
	endcase	
	case "WIREFRAME OFF"
		set object wireframe NET_Server.DATA_MapObj,0
	endcase
	endselect
	if left$(console_result$,15)="SET SERVER NAME"
		NET_NameServer(right$(console_result$,len(console_result$)-16))
	endif
	if left$(console_result$,14)="SET SERVER MAP"
		NET_Server.DATA_Map=right$(console_result$,len(console_result$)-15)
	endif
	if left$(console_result$,5)="SCALE"
		scale=val(right$(console_result$,len(console_result$)-6))
		scale object NET_Server.DATA_MapObj,scale,scale,scale
	endif
	if left$(console_result$,5)="PARSE"
		parse_file$=right$(console_result$,len(console_result$)-6)
		UI_SUB_ParseFile(parse_file$)
	endif
Endfunction

function UI_SUB_ParseFile(file$):
	// -----------------------
	// Parser function
	// -----------------------
	`Parses a script file
	if file exist(file$)>0:
		global TEMP_file
		TEMP_file=RES_grab(RES_File)
		open to read TEMP_file,file$
		repeat
			read string TEMP_file,line$
			UI_SUB_CheckConsoleCommand(line$)
		until file end(TEMP_file)>0
		close file TEMP_file
		RES_free(TEMP_file,RES_File)
	endif
endfunction
