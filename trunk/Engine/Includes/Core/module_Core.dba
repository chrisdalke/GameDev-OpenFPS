//////////////////////////////////////////////////////////////
//   OpenFPS
//   MAIN SOURCE
//////////////////////////////////////////////////////////////
//
// ----- Changelog:
// (11/19/09) - BMacZero - Created.
// (01/17/13) - thenerd  - Big project revival. 
//
// ----- To-Do / Notes:
// This is the main source file for the OpenFPS Project.
//
//////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////
// Setup program chunks
// This serves as organization. Each chunk is a list of functions.
// The function CORE_Run_Chunk() will run the list of functions
// within the specified chunk. In the future, options will be
// created allowing a function chunk to be run in multithreaded
// or debug mode.
//////////////////////////////////////////////////////////////

Dim Chunk_Init() As Dword
Dim Chunk_Update() as dword
Dim Chunk_Terminate() As dword

Global Chunk_Init_PTR as dword : Chunk_Init_PTR = get arrayptr(Chunk_Init()) : unlink array Chunk_Init()
Global Chunk_Update_PTR as dword : Chunk_Update_PTR = get arrayptr(Chunk_Update()) : unlink array Chunk_Update()
global Chunk_Terminate_PTR as dword : Chunk_Terminate_PTR = get arrayptr(Chunk_Terminate()) : unlink array Chunk_Terminate()

//Init chunk
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"SYS_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"TBM_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"UI_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"UI_Menu_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"IO_Input_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"Physics_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"World_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"Camera_Init")
Chunk_Init_PTR=CORE_Chunk_Add(Chunk_Init_PTR,"Network_Init")

//Update chunk
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"TBM_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"IO_Input_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"World_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"Terrain_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"Camera_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"Physics_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"UI_Menu_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"UI_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"Network_Update")
Chunk_Update_PTR=CORE_Chunk_Add(Chunk_Update_PTR,"SYS_Sync")

//Terminate chunk
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"Network_Terminate")
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"TBM_Terminate")
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"UI_Terminate")
Chunk_Terminate_PTR=CORE_Chunk_Add(Chunk_Terminate_PTR,"SYS_Terminate")

//////////////////////////////////////////////////////////////
// Init
//////////////////////////////////////////////////////////////

//runs the init set of functions
CORE_Chunk_Run(Chunk_Init_PTR)

//////////////////////////////////////////////////////////////
// Main loop
//////////////////////////////////////////////////////////////

Do
	//runs the update set of functions
	CORE_Chunk_Run(Chunk_Update_PTR)
	
	if Game.Terminate = 1 then exit
	
loop

//////////////////////////////////////////////////////////////
// Terminate
//////////////////////////////////////////////////////////////

//runs the terminate set of functions and then exits
CORE_Chunk_Run(Chunk_Terminate_PTR) : End

//////////////////////////////////////////////////////////////
// CHUNK Functions
//////////////////////////////////////////////////////////////

//CORE_Chunk_Add - Adds a function to the PTR chunk specified.
Function CORE_Chunk_Add(chunkptr As dword,functionname as string):
	local newchunkptr as dword
   local dim temp_chunk() as dword
   Link Array temp_chunk(),chunkptr
	Array Insert At Bottom temp_chunk()
	newchunkptr = get arrayptr(temp_chunk())
	temp_chunk()=Get Ptr To Function(functionname)
   unlink array temp_chunk()
Endfunction newchunkptr

//CORE_Chunk_Run - Executes the specified chunk of functions.
Function CORE_Chunk_Run(chunkptr As dword):
   Local Dim temp_chunk() As dword
   Link Array temp_chunk(),chunkptr
	For func = 0 To Array Count(temp_chunk()):
		//execute function string for Chunk(func)
		Call Function Ptr (temp_chunk(func))
	Next func
   unlink array temp_chunk()
   Undim temp_chunk()
endfunction

//////////////////////////////////////////////////////////////
// End of code
//////////////////////////////////////////////////////////////