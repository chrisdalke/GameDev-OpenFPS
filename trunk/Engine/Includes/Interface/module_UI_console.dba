//////////////////////////////////////////////////////////////
//  Game Engine Standard Modules - UI CONSOLE MODULE
//  Concept and code by thenerd, Summer 2012!
//////////////////////////////////////////////////////////////
//console key is `
//////////////////////////////////////////////////////////////
//  UDTS
//////////////////////////////////////////////////////////////

//console type
Type t_Console
	created as boolean
	active as boolean
	height As float
	bgcolor As Integer
	maxchatlines As Integer
Endtype

//console text line type
Type t_ConsoleLine
	prefix as string
	prefixcolormodifier As String
	Messagetext As String
	timestamp as integer
endtype

//////////////////////////////////////////////////////////////
//  FUNCTIONS
//////////////////////////////////////////////////////////////

//UI_Console_Init() - starts up the console system.
Function UI_Console_Init():
	Global Console As t_Console
	Console.created = 1
	Console.active=0
	Console.height=160
	Console.bgcolor=d3d_rgba(60,60,60,150)
	Console.maxchatlines=Console.height/16
	Dim Consolelines(8) As t_ConsoleLine
	Global cursor_toggle As Integer
	Global input_toggler As Boolean
	global mouse_laststate as integer
Endfunction

//UI_Console_AddLine() - Adds a line to the console.
Function UI_Console_AddLine(prefix As string, messagestring As string):
	if Console.created = 1:
	Array Delete Element Consolelines(),0
	array insert at bottom ConsoleLines()
	Consolelines(7).prefix=prefix
	Consolelines(7).prefixcolormodifier="~b"
	Consolelines(7).Messagetext=messagestring
	Consolelines(7).timestamp=hitimer(1)
	endif
Endfunction

//UI_Console_RunCMD() - Runs an engine function from the console.
Function UI_Console_RunCMD(commandstring As string):
	If Len(commandstring)>3:
	   select commandstring
	       case "load"
	           filenamenew$=openDialog("Load Level File","Level Files (*.xlvl)|*.xlvl",0,"Media\Levels\") 
            lastslash = 0
            for i = 0 to len(filenamenew$):
                if mid$(filenamenew$,i)="\" then lastslash = i
            next i
            new_filename$ = right$(filenamenew$,len(filenamenew$) - lastslash)
	           ui_writelog(UI_Blue,"Trying to load level file: "+new_filename$)
            if len(new_filename$)>3:
                Game.editor_flag = 1
                `Game_Load(new_filename$)
            endif
	       endcase
	       case "limit_debug 30"
            sync rate 30
	       endcase
        case "limit_debug 60"
            sync rate 60
        endcase
        case "limit_debug 0"
            sync rate 0
        endcase
	       case "quit"
	           Game.Terminate= 1
	       endcase
	       case "terminate"
           Game.Terminate= 1
	       endcase
	       case "exit"
           Game.Terminate= 1
	       endcase
	       case "disconnect"
            Trigger_Load("Network_Disconnect","")
	       endcase
	       case "cl_testconnect"
            Trigger_Load("Network_Connect","OpenFPS_173.48.153.103_25565_TestServer")
	       endcase
	       case "cl_testmessage"
	           Net_Client_Send_TCP_Message("cl_testmessage","null")
	       endcase
	       case "sv_testmessage"
            Net_Server_Send_TCP_Message("sv_testmessage","~p HELLO WORLD!")
	       endcase
	    endselect
	endif
	if left$(commandstring,5) = "chat "
    Net_Client_Send_TCP_Message("cl_chat","~b Client:~w "+right$(commandstring,len(commandstring)-5))
	endif
endfunction

//UI_Console_Update() - Updates the console. Messy but it works!
Function UI_Console_Update():
	//console key is `
	If Keystate(Game_Input.KEY_CONSOLE) And input_toggler=0: 
		Console.active=1-Console.active
		input_toggler=1
		Input_Text_Clear()
		mouse_laststate = UI_Mouse.Hidden
		If Console.active=1:
			UI_ShowMouse()
		else
			if mouse_laststate = UI_True then UI_HideMouse()
			if mouse_laststate = UI_False then UI_ShowMouse()
		endif
	endif
	If Keystate(Game_Input.KEY_CONSOLE)=0 Then input_toggler=0
	
	ui_putwindow(0-ui_mywindow.xwidth,0-(ui_mywindow.yrow*3),(Screen.Width/ui_mywindow.xwidth)+1,(Console.height/Ui_mywindow.yrow)+1)
	If Console.active=1:
		
		Console.height=clamp(Console.height+(20*TBM),0,200)
		UI_AlphaText(1,4,-200+Console.height+16,"~r[ ~b OPENFPS ENGINE revision "+Game.revision+" ~r Frametime: "+str$(Game.frametimeavg)+" ms ]",250)
		For i = 2 To 8:
			UI_AlphaText(1,4,-200+Console.height+i*16,ConsoleLines(i-1).prefixcolormodifier+"["+Consolelines(i-1).prefix+"]~w "+ConsoleLines(i-1).Messagetext,250)
		Next i
		If Ticker(QtrSecondTicker) then cursor_toggle=1-cursor_toggle
		If cursor_toggle=1 Then cursor$="_"
		if cursor_toggle=0 then cursor$=" "
		UI_AlphaText(1,4,-200+Console.height+9*16,"~rOPENFPS Console >>>~w "+ Input_Text_Grab()+cursor$,255)
		if returnkey() or asc(inkey$())=13 then UI_Console_RunCMD(Input_Text_Grab()):Input_Text_Clear()
	else
		Console.height=clamp(Console.height-(20*TBM),0,200)
	Endif
	`d3d_box 0,0,Screen.Width,Console.Height,Console.bgcolor
Endfunction

//////////////////////////////////////////////////////////////
//  CONSOLE FUNCTIONS
//////////////////////////////////////////////////////////////

//Quit() - quits the game.
Function quit():
	Game.Terminate = 1
Endfunction

//////////////////////////////////////////////////////////////
//  End of code.
//////////////////////////////////////////////////////////////